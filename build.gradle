buildscript {
	repositories {
		// mavenLocal()
		mavenCentral()
		jcenter()
		// maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
	}

	dependencies {
		classpath 'org.anarres.gradle:gradle-stdproject-plugin:1.0.9'
	}
}

ext {
	springVersion = "4.1.5.RELEASE"
}

apply plugin: 'org.anarres.stdproject'
stdproject {
	javadocLink "https://shevek.github.io/simple-xml/docs/javadoc/"
}

subprojects {
	group = "org.anarres.simplexml.serializers"

	apply plugin: 'org.anarres.stdmodule'
	stdmodule {
		description "Extra serializers and utility plugins for SimpleXML."
		author id: 'shevek', name: 'Shevek', email: 'github@anarres.org'
		license 'Apache-2.0'
	}

	dependencies {
		compile 'com.google.code.findbugs:annotations:2.0.3'
		compile 'org.slf4j:slf4j-api:1.7.12'
	}

	sourceCompatibility = 1.6
}

project(':simple-xml-serializers-jdk') {
	dependencies {
		// compile 'org.simpleframework:simple-xml:2.7.1'
		compile "org.anarres.mirrors.simpleframework:simple-xml:2.7.1-P2"
		compile 'com.google.guava:guava:18.0'
		compile 'com.google.auto.service:auto-service:1.0-rc2'
		compile 'org.anarres.typeserializer:typeserializer-core:1.0.7'

		testCompile project(':simple-xml-serializers-test')
	}
}

project(':simple-xml-serializers-thrift') {
	dependencies {
		compile project(':simple-xml-serializers-jdk')
		compile 'org.apache.thrift:libthrift:0.9.1'

		testCompile project(':simple-xml-serializers-test')
		// This is extraordinarily expensive. Is there a ThriftTestObj somewhere else?
		testCompile 'org.apache.hive:hive-serde:1.1.0'
	}
}

project(':simple-xml-serializers-dhcp4j') {
	dependencies {
		compile project(':simple-xml-serializers-jdk')
		compile 'org.anarres.dhcp:dhcp-protocol:1.0.5'

		testCompile project(':simple-xml-serializers-test')
	}
}

project(':simple-xml-serializers-spring') {
	dependencies {
		compile project(':simple-xml-serializers-jdk')
		compile "org.springframework:spring-context:$springVersion"

		testCompile project(':simple-xml-serializers-test')
		testCompile "org.springframework:spring-test:$springVersion"
	}
}

project(':simple-xml-serializers-spring-remote') {
	dependencies {
		compile project(':simple-xml-serializers-jdk')
		compile "org.springframework:spring-web:$springVersion"
		compile 'javax.servlet:javax.servlet-api:3.1.0'

		testCompile project(':simple-xml-serializers-test')
		testCompile "org.springframework:spring-test:$springVersion"
	}
}

project(':simple-xml-serializers-test') {
	dependencies {
		compile project(':simple-xml-serializers-jdk')
		compile 'junit:junit:4.12'
	}
}
